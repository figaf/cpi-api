plugins {
    id 'java-library'
    id 'maven-publish'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

group = 'com.github.figaf'
version = '1.3'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    mavenLocal()
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    api 'com.github.figaf:integration-commons:1.1'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.httpcomponents:httpmime:4.5.3'

    testCompileOnly 'org.projectlombok:lombok:1.18.8'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.8'

    testImplementation 'com.github.figaf:integration-commons-test:1.2'
    testImplementation 'org.assertj:assertj-core:3.18.0'
    testImplementation 'org.mockito:mockito-core:3.6.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()

    project.properties.each {propertyName, propertyValue ->
        if (!propertyName.startsWith("agent-test-data")) return

        systemProperty propertyName, propertyValue
    }

    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
    }

    maxHeapSize = "2G"
}